#[
    This script header is a part of the RailDriver project.

    RailDriver. Smart Locomotive Control script for Garry's Mod Train Build Servers.
    Copyright Â© 2022, Cassandra "ZZ Cat" Robinson. All rights reserved.

    This E2 script is free software: you can redistribute it and/or modify
    it under the terms of the GNU Affero General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This E2 script is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU Affero General Public License for more details.

    You should have received a copy of the GNU Affero General Public License
    along with this E2 script.  If not, see <https://www.gnu.org/licenses/>.
]#

@name RailDriver/lib/controls/playerControls
@persist PCDirection PCThrottle PCBrake PCEmergencyBrake PCHorn PCHeadlights PCDitchLights
@persist PCTrainDriver:entity

# This function initializes the player controls.
# This function should be called once per player.
function table playerControlsInit(KeyboardControls:table, ThrottleMap:table, BrakeMap:table, TrainDriver:entity)
{
    local Controls = table()
    Controls["Direction", number] = 0
    Controls["Throttle", number] = 0
    Controls["Brake", number] = 0
    Controls["EmergencyBrake", number] = 0
    Controls["Horn", number] = 0
    Controls["Bell", number] = 0
    Controls["Headlights", number] = 0
    Controls["DitchLights", number] = 0
    Controls["TrainDriver", entity] = TrainDriver
    Controls["Controls", table] = KeyboardControls

    # 'table:toArray()' is bugged, so I am directly using the throttle and brake maps.
    Controls["ThrottleMap", table] = ThrottleMap
    Controls["BrakeMap", table] = BrakeMap

    # Fault Reporting for the player controls.
    Controls["Faults", table] = table()
    Controls["Faults", table]["Direction", number] = 0
    Controls["Faults", table]["Throttle", number] = 0
    Controls["Faults", table]["Brake", number] = 0
    Controls["Faults", table]["EmergencyBrake", number] = 0
    Controls["Faults", table]["Message", string] = ""

    runOnKeys(Controls["TrainDriver", entity], 1)
    return Controls
}

# This function updates the player controls.
# This function should be called every key clock tick.
function void playerControlsUpdate(Controls:table)
{
    local KeyPressed = keyClkPressed()
    local KeyState = keyClk(Controls["TrainDriver", entity]) == 1 ? 1 : 0

    local String = ""
    String += "Key: " + KeyPressed + ", State: " + KeyState
    #printColor(vec(0, 255, 0), "[RailDriver | DEBUG]", vec(255, 255, 255), ": " + String)

    if (Controls["TrainDriver", entity]:inVehicle())
    {
        if (KeyPressed)
        {
            # Set direction to forward if the forward key is pressed.
            if (KeyState == 1 & KeyPressed == Controls["Controls", array][1, string])
            {
                Controls["Direction", number] = Controls["Direction", number] + 1
                Controls["Direction", number] = clamp(Controls["Direction", number], -1, 1)
            }

            # Set direction to reverse if the reverse key is pressed.
            if (KeyState == 1 & KeyPressed == Controls["Controls", array][2, string])
            {
                Controls["Direction", number] = Controls["Direction", number] - 1
                Controls["Direction", number] = clamp(Controls["Direction", number], -1, 1)
            }

            # Increase throttle if the increase throttle key is pressed.
            if (KeyState == 1 & KeyPressed == Controls["Controls", array][3, string])
            {
                # Increase throttle by 1.
                Controls["Throttle", number] = Controls["Throttle", number] + 1

                # Get the throttle map.
                local ThrMap = Controls["ThrottleMap", table]

                # Clamp the throttle to the maximum value in the throttle map.
                # Each throttle map has its own minimum & maximum values.
                switch(ThrMap["Throttle Input Type", string])
                {
                    # Velocity Mode.
                    case "Velocity",
                        Controls["Throttle", number] = clamp(Controls["Throttle", number], 0, ThrMap["Velocity Setpoint", array]:count())
                    break

                    # Acceleration Mode.
                    case "Acceleration",
                        Controls["Throttle", number] = clamp(Controls["Throttle", number], 0, ThrMap["Acceleration Setpoint", array]:count())
                    break

                    # Torque Mode.
                    case "Torque",
                        Controls["Throttle", number] = clamp(Controls["Throttle", number], 0, ThrMap["Torque Setpoint", array]:count())
                    break
                }
            }

            # Decrease throttle if the decrease throttle key is pressed.
            if (KeyState == 1 & KeyPressed == Controls["Controls", array][4, string])
            {
                # Decrease throttle by 1.
                Controls["Throttle", number] = Controls["Throttle", number] - 1

                # Get the throttle map.
                local ThrMap = Controls["ThrottleMap", table]

                # Clamp the throttle to the minimum value in the throttle map.
                # Each throttle map has its own minimum & maximum values.
                switch(ThrMap["Throttle Input Type", string])
                {
                    # Velocity Mode.
                    case "Velocity",
                        Controls["Throttle", number] = clamp(Controls["Throttle", number], 0, ThrMap["Velocity Setpoint", array]:count())
                    break

                    # Acceleration Mode.
                    case "Acceleration",
                        Controls["Throttle", number] = clamp(Controls["Throttle", number], 0, ThrMap["Acceleration Setpoint", array]:count())
                    break

                    # Torque Mode.
                    case "Torque",
                        Controls["Throttle", number] = clamp(Controls["Throttle", number], 0, ThrMap["Torque Setpoint", array]:count())
                    break
                }
            }

            # Increase brake if the increase brake key is pressed.
            if (KeyState == 1 & KeyPressed == Controls["Controls", array][5, string])
            {
                # Increase brake by 1.
                Controls["Brake", number] = Controls["Brake", number] + 1

                # Get the brake map.
                local BrkMap = Controls["BrakeMap", table]

                # Clamp the brake to the maximum value in the brake map.
                # Each brake map has its own minimum & maximum values.
                switch(BrkMap["Brake Input Type", string])
                {
                    # Velocity Mode.
                    case "Velocity",
                        # Brake is disabled in this mode.
                        Controls["Brake", number] = clamp(Controls["Brake", number], 0, 0)
                    break

                    # Acceleration Mode.
                    case "Acceleration",
                        Controls["Brake", number] = clamp(Controls["Brake", number], 0, BrkMap["Deceleration Setpoint", array]:count())
                    break

                    # Torque Mode.
                    case "Torque",
                        Controls["Brake", number] = clamp(Controls["Brake", number], 0, BrkMap["Torque Setpoint", array]:count())
                    break
                }
            }

            # Decrease brake if the decrease brake key is pressed.
            if (KeyState == 1 & KeyPressed == Controls["Controls", array][6, string])
            {
                # Decrease brake by 1.
                Controls["Brake", number] = Controls["Brake", number] - 1

                # Get the brake map.
                local BrkMap = Controls["BrakeMap", table]

                # Clamp the brake to the minimum value in the brake map.
                # Each brake map has its own minimum & maximum values.
                switch(BrkMap["Brake Input Type", string])
                {
                    # Velocity Mode.
                    case "Velocity",
                        # Brake is disabled in this mode.
                        Controls["Brake", number] = clamp(Controls["Brake", number], 0, 0)
                    break

                    # Acceleration Mode.
                    case "Acceleration",
                        Controls["Brake", number] = clamp(Controls["Brake", number], 0, BrkMap["Deceleration Setpoint", array]:count())
                    break

                    # Torque Mode.
                    case "Torque",
                        Controls["Brake", number] = clamp(Controls["Brake", number], 0, BrkMap["Torque Setpoint", array]:count())
                    break
                }
            }

            # Set emergency brake if the emergency brake key is pressed.
            if (KeyState == 1 & KeyPressed == Controls["Controls", array][7, string])
            {
                Controls["EmergencyBrake", number] = 1
            }

            # Set horn if the horn key is pressed.
            if (KeyState == 1 & KeyPressed == Controls["Controls", array][8, string])
            {
                Controls["Horn", number] = 1
            }

            # Release horn if the horn key is released.
            if (KeyState == 0 & KeyPressed == Controls["Controls", array][8, string])
            {
                Controls["Horn", number] = 0
            }

            # Toggle bell if the bell key is pressed.
            if (KeyState == 1 & KeyPressed == Controls["Controls", array][9, string])
            {
                Controls["Bell", number] = Controls["Bell", number] == 0 ? 1 : 0
            }

            # Toggle headlights if the headlights key is pressed.
            if (KeyState == 1 & KeyPressed == Controls["Controls", array][10, string])
            {
                Controls["Headlights", number] = Controls["Headlights", number] == 0 ? 1 : 0
            }

            # Toggle ditch lights if the ditch lights key is pressed.
            if (KeyState == 1 & KeyPressed == Controls["Controls", array][11, string])
            {
                Controls["DitchLights", number] = Controls["DitchLights", number] == 0 ? 1 : 0
            }
        }
    }
}

# This function returns the direction of the locomotive.
function number playerControlsGetDirection(Controls:table)
{
    return Controls["Direction", number]
}

# This function returns the throttle of the locomotive.
function number playerControlsGetThrottle(Controls:table)
{
    return Controls["Throttle", number]
}

# This function returns the brake of the locomotive.
function number playerControlsGetBrake(Controls:table)
{
    return Controls["Brake", number]
}

# This function returns the emergency brake of the locomotive.
function number playerControlsGetEmergencyBrake(Controls:table)
{
    return Controls["EmergencyBrake", number]
}

# This function returns the horn of the locomotive.
function number playerControlsGetHorn(Controls:table)
{
    return Controls["Horn", number]
}

# This function returns the bell of the locomotive.
function number playerControlsGetBell(Controls:table)
{
    return Controls["Bell", number]
}

# This function returns the headlights of the locomotive.
function number playerControlsGetHeadlights(Controls:table)
{
    return Controls["Headlights", number]
}

# This function returns the ditch lights of the locomotive.
function number playerControlsGetDitchLights(Controls:table)
{
    return Controls["DitchLights", number]
}

# This function checks whether or not the player controls are valid.
function number playerControlsValid(Controls:table, Speed)
{
    # Get the direction, throttle, brake, and emergency brake.
    local Direction = Controls["Direction", number]
    local Throttle = Controls["Throttle", number]
    local Brake = Controls["Brake", number]
    local EmergencyBrake = Controls["EmergencyBrake", number]

    # Get the throttle & brake maps.
    local ThrMap = Controls["ThrottleMap", table]
    local BrkMap = Controls["BrakeMap", table]

    # Get the throttle & brake input types.
    local ThrInputType = ThrMap["Throttle Input Type", string]
    local BrkInputType = BrkMap["Brake Input Type", string]

    # Direction cannot be set higher than 1 or lower than -1.
    if (Direction > 1)
    {
        Controls["Direction", number] = 1
        Controls["Faults", table]["Message", string] = "Direction is already set to forward."
        Controls["Faults", table]["Direction", number] = 1
        return 0
    }

    elseif (Direction < -1)
    {
        Controls["Direction", number] = -1
        Controls["Faults", table]["Message", string] = "Direction is already set to reverse."
        Controls["Faults", table]["Direction", number] = 1
        return 0
    }

    # Direction must remain set to forward when the locomotive is moving forward,
    # and direction must remain set to reverse when the locomotive is moving in reverse.
    if (Direction != 1 & Speed > 0)
    {
        Controls["Direction", number] = 1
        Controls["Faults", table]["Message", string] = "Cannot set direction whilst the locomotive is moving."
        Controls["Faults", table]["Direction", number] = 1
        return 0
    }

    elseif (Direction != -1 & Speed > 0)
    {
        Controls["Direction", number] = -1
        Controls["Faults", table]["Message", string] = "Cannot set direction whilst the locomotive is moving."
        Controls["Faults", table]["Direction", number] = 1
        return 0
    }

    # Throttle is invalid when the locomotive's direction is neutral.
    if (Throttle > 0 & Direction == 0)
    {
        Controls["Throttle", number] = 0
        Controls["Faults", table]["Message", string] = "Throttle is invalid when the locomotive's direction is neutral."
        Controls["Faults", table]["Throttle", number] = 1
        return 0
    }

    # Throttle is invalid when its value is higher than or less than the size of the throttle maps for each throttle input type.
    switch(ThrInputType)
    {
        # Velocity Mode.
        case "Velocity",
            # Throttle cannot be set higher than the maximum size of the Throttle Map.
            if (Throttle > ThrMap["Velocity Setpoint", array]:count())
            {
                Controls["Throttle", number] = ThrMap["Velocity Setpoint", array]:count()
                Controls["Faults", table]["Message", string] = "Throttle is already set to maximum."
                Controls["Faults", table]["Throttle", number] = 1
                return 0
            }

            # Throttle cannot be set lower than 0.
            elseif (Throttle < 0)
            {
                Controls["Throttle", number] = 0
                Controls["Faults", table]["Message", string] = "Throttle is already set to minimum."
                Controls["Faults", table]["Throttle", number] = 1
                return 0
            }
        break

        # Acceleration Mode.
        case "Acceleration",
            # Throttle cannot be set higher than the maximum size of the Throttle Map.
            if (Throttle > ThrMap["Acceleration Setpoint", array]:count())
            {
                Controls["Throttle", number] = ThrMap["Acceleration Setpoint", array]:count()
                Controls["Faults", table]["Message", string] = "Throttle is already set to maximum."
                Controls["Faults", table]["Throttle", number] = 1
                return 0
            }

            # Throttle cannot be set lower than 0.
            elseif (Throttle < 0)
            {
                Controls["Throttle", number] = 0
                Controls["Faults", table]["Message", string] = "Throttle is already set to minimum."
                Controls["Faults", table]["Throttle", number] = 1
                return 0
            }
        break

        # Torque Mode.
        case "Torque",
            # Throttle cannot be set higher than the maximum size of the Throttle Map.
            if (Throttle > ThrMap["Torque Setpoint", array]:count())
            {
                Controls["Throttle", number] = ThrMap["Torque Setpoint", array]:count()
                Controls["Faults", table]["Message", string] = "Throttle is already set to maximum."
                Controls["Faults", table]["Throttle", number] = 1
                return 0
            }

            # Throttle cannot be set lower than 0.
            elseif (Throttle < 0)
            {
                Controls["Throttle", number] = 0
                Controls["Faults", table]["Message", string] = "Throttle is already set to minimum."
                Controls["Faults", table]["Throttle", number] = 1
                return 0
            }
        break
    }

    # Brake is invalid when its value is higher than or less than the size of the brake maps for each brake input type.
    switch(BrkInputType)
    {
        # Velocity Mode.
        case "Velocity",
            # Brake is disabled in Velocity Mode. Any value set to the brake is invalid.
            if (Brake > 0)
            {
                Controls["Brake", number] = 0
                Controls["Faults", table]["Message", string] = "Brake is disabled in Velocity Mode."
                Controls["Faults", table]["Brake", number] = 1
                return 0
            }
        break

        # Acceleration Mode.
        case "Acceleration",
            # Brake cannot be set higher than the maximum size of the Brake Map.
            if (Brake > BrkMap["Acceleration Setpoint", array]:count())
            {
                Controls["Brake", number] = BrkMap["Deceleration Setpoint", array]:count()
                Controls["Faults", table]["Message", string] = "Brake is already set to maximum."
                Controls["Faults", table]["Brake", number] = 1
                return 0
            }

            # Brake cannot be set lower than 0.
            elseif (Brake < 0)
            {
                Controls["Brake", number] = 0
                Controls["Faults", table]["Message", string] = "Brake is already set to minimum."
                Controls["Faults", table]["Brake", number] = 1
                return 0
            }
        break

        # Torque Mode.
        case "Torque",
            # Brake cannot be set higher than the maximum size of the Brake Map.
            if (Brake > BrkMap["Torque Setpoint", array]:count())
            {
                Controls["Brake", number] = BrkMap["Torque Setpoint", array]:count()
                Controls["Faults", table]["Message", string] = "Brake is already set to maximum."
                Controls["Faults", table]["Brake", number] = 1
                return 0
            }
        break
    }

    # Return 1 if all checks have passed.
    return 1
}

# This function returns the fault message.
function string playerControlsGetFaultMessage(Controls:table)
{
    return Controls["Faults", table]["Message", string]
}

# This function returns the direction fault.
function number playerControlsGetDirectionFault(Controls:table)
{
    return Controls["Faults", table]["Direction", number]
}

# This function returns the throttle fault.
function number playerControlsGetThrottleFault(Controls:table)
{
    return Controls["Faults", table]["Throttle", number]
}

# This function returns the brake fault.
function number playerControlsGetBrakeFault(Controls:table)
{
    return Controls["Faults", table]["Brake", number]
}

# This function clears the fault message.
# Returns true if the fault message was cleared.
# False if no fault message was found.
function number playerControlsClearFaultMessage(Controls:table)
{
    if (Controls["Faults", table]["Message", string] != "")
    {
        Controls["Faults", table]["Message", string] = ""
        return 1
    }
    else
    {
        Controls["Faults", table]["Message", string] = "No fault message found."
        return 0
    }
}

# This function clears the direction fault.
# Returns true if the direction fault was cleared.
# False if the direction fault was not previously set.
function number playerControlsClearDirectionFault(Controls:table)
{
    if (Controls["Faults", table]["Direction", number] == 1)
    {
        Controls["Faults", table]["Direction", number] = 0
        return 1
    }
    else
    {
        Controls["Faults", table]["Message", string] = "Direction fault was not previously set."
        return 0
    }
}

# This function clears the throttle fault.
# Returns true if the throttle fault was cleared.
# False if the throttle fault was not previously set.
function number playerControlsClearThrottleFault(Controls:table)
{
    if (Controls["Faults", table]["Throttle", number] == 1)
    {
        Controls["Faults", table]["Throttle", number] = 0
        return 1
    }
    else
    {
        Controls["Faults", table]["Message", string] = "Throttle fault was not previously set."
        return 0
    }
}

# This function clears the brake fault.
# Returns true if the brake fault was cleared.
# False if the brake fault was not previously set.
function number playerControlsClearBrakeFault(Controls:table)
{
    if (Controls["Faults", table]["Brake", number] == 1)
    {
        Controls["Faults", table]["Brake", number] = 0
        return 1
    }
    else
    {
        Controls["Faults", table]["Message", string] = "Brake fault was not previously set."
        return 0
    }
}
