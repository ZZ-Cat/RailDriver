#[
    This script header is a part of RailDriver.

    RailDriver. Smart Locomotive Control script for Garry's Mod Train Build Servers.
    Copyright Â© 2022, Cassandra "ZZ Cat" Robinson. All rights reserved.

    This E2 script is free software: you can redistribute it and/or modify
    it under the terms of the GNU Affero General Public License as
    published by the Free Software Foundation, either version 3 of the
    License, or (at your option) any later version.

    This E2 script is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU Affero General Public License for more details.

    You should have received a copy of the GNU Affero General Public License
    along with this E2 script.  If not, see <https://www.gnu.org/licenses/>.
]#

@name RailDriver/lib/ota/local

#[
    Local OTA Updates library for RailDriver.

    This library is used to check for and apply local Over-The-Air updates to RailDriver.
    It accomplishes this by checking the local filesystem for a file named "version.json" &
    comparing its contents to the current version of RailDriver. If the file exists and its
    contents are different than the current version, the update is applied.
]#

# This function initializes the OTA library.
# It should be called once at the beginning of the script.
# @return Ota A handle to the OTA library.
function table otaLocalInit()
{
    local Ota = table()
    Ota["RailDriver E2", entity] = entity()
    Ota["Version File Path", string] = "e2shared/RailDriver/lib/ota/version.json"
    Ota["Clock ID", string] = "RailDriver OTA Clock"
    Ota["Clock Delay", number] = 0
    Ota["Upload Attempts", number] = 0
    Ota["Upload Max Attempts", number] = 5

    Ota["File Path to Load", string] = ""

    # Version file contents are stored here.
    Ota["RailDriver", table] = table()

    # Flags.
    Ota["Flags", table] = table()
    Ota["Flags", table]["File Upload Started", number] = 0
    Ota["Flags", table]["File Upload Complete", number] = 0
    Ota["Flags", table]["File Upload Failed", number] = 0
    Ota["Flags", table]["Version File is Valid", number] = 0

    Ota["Debug", table]["JSON", string] = ""

    return Ota
}

# This function starts the OTA Clock.
# It should be called once at the beginning of the script.
# @param Ota A handle to the OTA library.
# @param Interval The interval at which to attempt to load the version file.
# @return void
function void otaLocalStartClock(Ota:table, Interval:number)
{
    Ota["Clock Delay", number] = Interval
    timer(Ota["Clock ID", string], Ota["Clock Delay", number])
}

# This function stops the OTA Clock.
# @param Ota A handle to the OTA library.
# @return void
function void otaLocalStopClock(Ota:table)
{
    stoptimer(Ota["Clock ID", string])
}

# This function restarts the OTA Clock with the last used interval.
# @param Ota A handle to the OTA library.
# @return void
function void otaLocalRestartClock(Ota:table)
{
    otaLocalStopClock(Ota)
    otaLocalStartClock(Ota, Ota["Clock Delay", number])
}

# This function returns the clock ID.
# @param Ota A handle to the OTA library.
# @return string The clock ID.
function string otaLocalGetClockID(Ota:table)
{
    return Ota["Clock ID", string]
}

# This function starts loading the version file.
# It should be called when the script is first loaded.
# @param Ota A handle to the OTA library.
# @return void
function void otaLocalLoadVersionFile(Ota:table)
{
    Ota["File Path to Load", string] = ">" + Ota["Version File Path", string]
    Ota["Flags", table]["Version File is Valid", number] = 0
    runOnFile(1)
    otaLocalStartClock(Ota, 300)
}

# This function starts uploading RailDriver to the server.
# It should be called when the CLI is used to update RailDriver.
# @param Ota A handle to the OTA library.
# @return void
function number otaLocalUploadRailDriver(Ota:table)
{
    # Check if the Version File is Valid.
    if (Ota["Flags", table]["Version File is Valid", number] == 0 | Ota["Flags", table]["File Upload Failed", number] == 1)
    {
        return 0
    }

    local FilePath = Ota["RailDriver", table]["Main", table]["Path", string] + "/" + Ota["RailDriver", table]["Main", table]["File", string]
    Ota["File Path to Load", string] = ">" + FilePath

    # Debug.
    # print("Path: " + Ota["File Path to Load", string])

    runOnFile(1)
    otaLocalStartClock(Ota, 300)
    return 1
}

function void otaLocalLoadFileHandler(Ota:table)
{
    if (Ota["Flags", table]["File Upload Started", number] == 0)
    {
        if (fileCanLoad())
        {
            otaLocalStopClock(Ota)
            fileLoad(Ota["File Path to Load", string])
            Ota["Upload Attempts", number] = 0
            Ota["Flags", table]["File Upload Complete", number] = 0
            Ota["Flags", table]["File Upload Started", number] = 1
        }
        else
        {
            otaLocalStopClock(Ota)
            Ota["Upload Attempts", number] = Ota["Upload Attempts", number] + 1
            if (Ota["Upload Attempts", number] >= Ota["Upload Max Attempts", number])
            {
                Ota["Upload Attempts", number] = 0
                Ota["Flags", table]["File Upload Started", number] = 0
                Ota["Flags", table]["File Upload Complete", number] = 0
                Ota["Flags", table]["File Upload Failed", number] = 1
                runOnFile(0)
                error("Failed to load file.")
            }
            else
            {
                otaLocalStartClock(Ota, 1000)
            }
        }
    }
    else
    {
        error("File upload already started.")
    }
}

# This function is used to internally update the Version File's hash.
# @param Ota A handle to the OTA library.
# @param Data The entire file's contents.
# @param Hash The new hash.
# @return 1 if the hash was updated, 0 otherwise.
function number otaLocalUpdateVersionFileHash(Ota:table, Data:table, Hash:number)
{

    local T = table()
    T["Hash", string] = Hash:toString()
    Data["RailDriver", table] = Data["RailDriver", table]:add(T)
    #Data["RailDriver", table]["Hash", string] = Hash:toString()

    # Check if the hash exists.
    if (Data:exists("RailDriver") == 1)
    {
        if (Data["RailDriver", table]:exists("Hash") != 1)
        {
            error("Missing checksum.")
            return 0
        }
    }
    else
    {
        error("Missing RailDriver section.")
        return 0
    }

    # Check if the owner's SteamID exists.
    local OwnerSteamData = Data["RailDriver", table]["Developers", table]["Owner", table]["Social", table]["Steam", table]:clone()
    print("Owner Steam ID: " + Data["RailDriver", table]["Developers", table]["Owner", table]["Social", table]["Steam", table]["ID", string])
    if (OwnerSteamData:exists("ID") != 1 | OwnerSteamData["ID", string] == "")
    {
        error("Missing owner's SteamID.")
        return 0
    }

    # Encode Data to JSON.
    local JSON = jsonEncode(Data, 1)
    local JsonError = jsonError()
    if (JsonError != "")
    {
        error("Failed to encode JSON: " + JsonError)
        return 0
    }

    # Write JSON to file.
    if (fileCanWrite() != 1)
    {
        error("Failed to write to file: " + Ota["Version File Path", string])
        return 0
    }

    fileWrite(">" + Ota["Version File Path", string], JSON)

    return 1
}

# This function is used internally to check if the current player is a developer.
# It should not be called directly by RailDriver.
# @param Ota A handle to the OTA library.
# @param Data:table The data to use as a reference against the Player.
# @param Player:entity The player to check.
# @return number 1 if the player is a developer, 0 otherwise.
function number otaLocalPlayerIsDeveloper(Ota:table, Data:table, Player:entity)
{
    
    local ThisPerson = Player
    local ThisPersonSteamID = ThisPerson:steamID()
    local ThisPersonSteamID64 = steamIDTo64(ThisPersonSteamID)
    local ThisPersonName = ThisPerson:name()

    if (Data:exists("RailDriver") == 1 & Data["RailDriver", table]:exists("Developers") == 1 & Data["RailDriver", table]["Developers", table]:exists("Owner") == 1)
    {
        local ThisPersonTable = table()
        ThisPersonTable["Steam", table] = table()
        ThisPersonTable["Steam", table]["Username", string] = ThisPersonName
        ThisPersonTable["Steam", table]["ID", string] = ThisPersonSteamID
        ThisPersonTable["Steam", table]["ID64", string] = ThisPersonSteamID64

        local OwnerTable = table()
        OwnerTable["Steam", table] = table()

        # Use T:clone() to avoid modifying the original table.
        # This is important because the original table is used to update the version file,
        # & using T:removeTable(Index) globally removes the specified table from the original table.
        OwnerTable["Steam", table] = Data["RailDriver", table]["Developers", table]["Owner", table]["Social", table]["Steam", table]:clone()

        # This has been commented out due to the fact that the T:removeTable(Index) function actually removes the table from the original table.
        # OwnerTable["Steam", table] = Data["RailDriver", table]["Developers", table]["Owner", table]["Social", table]:removeTable("Steam")

        # Debug.
        # print("Owner Username: " + OwnerTable["Steam", table]["Username", string])
        # print("Owner SteamID: " + OwnerTable["Steam", table]["ID", string])
        # print("Owner SteamID64: " + OwnerTable["Steam", table]["ID64", string])
        # print("This Person Username: " + ThisPersonTable["Steam", table]["Username", string])
        # print("This Person SteamID: " + ThisPersonTable["Steam", table]["ID", string])
        # print("This Person SteamID64: " + ThisPersonTable["Steam", table]["ID64", string])

        # error("Stopping here, just 'cause I found where my bug is.")

        # Check if the player is the owner.
        # Check their Username, SteamID, and SteamID64.
        # If all three match, then they are the owner.
        if (ThisPersonTable["Steam", table]["Username", string] == OwnerTable["Steam", table]["Username", string] & ThisPersonTable["Steam", table]["ID", string] == OwnerTable["Steam", table]["ID", string] & ThisPersonTable["Steam", table]["ID64", string] == OwnerTable["Steam", table]["ID64", string])
        {
            # Debug.
            # print("This person is the owner."))

            return 1
        }
    }

    return 0
}

function number otaLocalIsVersionFileValid(Ota:table, RawData:string)
{
    local Lines = RawData:explode("\n")
    local HashFile = 0
    local HashCalculated = 0

    for (I = (Lines:count() - 4), Lines:count())
    {
        local Line = Lines[I, string]
        local LineTrimmed = Line:trim()
        local LineParts = LineTrimmed:explode(":")

        # Remove the quotes from both ends of the string.
        local LineName = LineParts[1, string]:sub(2, LineParts[1, string]:length() - 1)
        local LineValue = LineParts[2, string]:sub(2, LineParts[2, string]:length() - 1)

        if (LineName == "Hash")
        {
            # Debug.
            # print(LineName + ": " + LineValue)

            if (Lines[I - 1, string]:find(","))
            {
                HashFile = LineValue:toNumber()
                Lines[I - 1, string] = Lines[I - 1, string]:replace(",", "")
                Lines:removeString(I)
                break
            }
        }
    }

    # Check if the hash checksum from the file is valid.
    if (HashFile == 0)
    {
        error("Missing checksum in file: " + Ota["Version File Path", string])
    }

    local RawDataNoSum = ""
    for (I = 1, Lines:count())
    {
        RawDataNoSum += Lines[I, string] + "\n"
    }

    # Calculate the hash checksum.
    HashCalculated = hash(RawDataNoSum)

    # Debug.
    # print("Hash File: " + HashFile)
    # print("Hash Calculated: " + HashCalculated)

    Ota["Flags", table]["Version File is Valid", number] = (HashFile == HashCalculated) ? 1 : 0

    return Ota["Flags", table]["Version File is Valid", number]
}

function void otaLocalFileLoadedHandler(Ota:table)
{
    runOnFile(0)
    local FileStatus = fileStatus()
    
    Ota["Flags", table]["File Upload Started", number] = 0

    if (FileStatus == _FILE_OK)
    {
        Ota["Flags", table]["File Upload Failed", number] = 0

        local FileName = fileName()

        if (FileName == ">" + Ota["Version File Path", string])
        {
            Ota["Flags", table]["File Upload Complete", number] = 1

            local FileContents = fileRead()
            if (FileContents == "")
            {
                error("Version file is empty.")
            }
            
            local FileContentsTable = jsonDecode(FileContents)
            local JsonError = jsonError()
            if (JsonError != "")
            {
                error("Failed to decode version file: " + JsonError)
            }

            # Check if the version file is valid.
            if (otaLocalIsVersionFileValid(Ota, FileContents) != 1)
            {
                if (otaLocalPlayerIsDeveloper(Ota, FileContentsTable, owner()) == 1)
                {
                    error("Version file is invalid.\nEnsure that the file is not corrupted, and that the checksum is correct.")
                }
                else
                {
                    error("Version file is invalid.")
                }
            }

            # Store the file contents in the OTA handle.
            if (FileContentsTable:exists("RailDriver") == 1)
            {
                Ota["RailDriver", table] = FileContentsTable["RailDriver", table]:clone()
            }
            else
            {
                error("RailDriver section is missing from version file.")
            }
            #Ota["RailDriver", table] = FileContentsTable["RailDriver", table]:clone()
        }
        elseif (FileName == ">" + Ota["RailDriver", table]["Main", table]["Path", string] + "/" + Ota["RailDriver", table]["Main", table]["File", string])
        {
            Ota["Flags", table]["File Upload Complete", number] = 1

            local FileContents = fileRead()
            if (FileContents == "")
            {
                error(Ota["RailDriver", table]["Main", table]["File", string] + " is empty.")
            }

            # Debug.
            #print("RailDriver uploaded successfully, but not yet installed.")
            #print(Ota["RailDriver", table]["Main", table]["File", string] + " Hash: " + hash(FileContents))
            #print("RailDriver Hash: " + hash(getCode()))

            # Update RailDriver
            local E = entity()
            E:remoteUpload(Ota["RailDriver", table]["Main", table]["Path", string] + "/" + Ota["RailDriver", table]["Main", table]["File", string])
        }
        else
        {
            Ota["Flags", table]["File Upload Complete", number] = 0
            Ota["Flags", table]["File Upload Failed", number] = 1
            error("Loaded file path mismatch.")
        }
    }
    elseif (FileStatus == _FILE_UNKNOWN)
    {
        Ota["Flags", table]["File Upload Failed", number] = 1
        error("Unknown error while loading file.")
    }
    elseif (FileStatus == _FILE_TIMEOUT)
    {
        Ota["Flags", table]["File Upload Failed", number] = 1
        error("Loading file timed out.")
    }
    elseif (FileStatus == _FILE_404)
    {
        Ota["Flags", table]["File Upload Failed", number] = 1
        error("File not found.")
    }
    elseif (FileStatus == _FILE_TRANSFER_ERROR)
    {
        Ota["Flags", table]["File Upload Failed", number] = 1
        error("Failed to transfer file.")
    }
}

# This function returns the About section of RailDriver's version file.
# @param Ota A handle to the OTA library.
# @return string The About section of RailDriver's version file.
function string otaLocalGetAboutMessage(Ota:table)
{
    local Str = ""

    if (Ota["Flags", table]["Version File is Valid", number] == 1 & Ota["Flags", table]["File Upload Failed", number] == 0)
    {
        Str += Ota["RailDriver", table]["About", table]["Name", string] + "\n"
        Str += Ota["RailDriver", table]["About", table]["Description", string] + "\n"
        Str += "Version:      " + Ota["RailDriver", table]["Main", table]["Version", string] + "\n"
        Str += "Author:       " + Ota["RailDriver", table]["About", table]["Author", string] + "\n"
        Str += "Copyright:    " + Ota["RailDriver", table]["About", table]["Copyright", string] + "\n"
        Str += "License:      " + Ota["RailDriver", table]["About", table]["License", string] + "\n"
        Str += "Source Code:  " + Ota["RailDriver", table]["About", table]["Source Code", string] + "\n"
    }
    else
    {
        Str += "Unable to retrieve version information."
    }

    return Str
}

function string otaLocalDebugGetVersionFileContents(Ota:table)
{
    return Ota["Debug", table]["JSON", string]
}
