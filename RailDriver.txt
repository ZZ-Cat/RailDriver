#[
    RailDriver. Smart Locomotive Control script for Garry's Mod Train Build Servers.
    Copyright Â© 2022, Cassandra "ZZ Cat" Robinson. All rights reserved.

    This E2 script is free software: you can redistribute it and/or modify
    it under the terms of the GNU Affero General Public License as
    published by the Free Software Foundation, either version 3 of the
    License, or (at your option) any later version.

    This E2 script is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU Affero General Public License for more details.

    You should have received a copy of the GNU Affero General Public License
    along with this E2 script.  If not, see <https://www.gnu.org/licenses/>.
]#

@name RailDriver
@model models/beer/wiremod/gate_e2_nano.mdl
@persist Trucks:array Speedo:table
@persist [PlayerControls ThrottleMap BrakeMap]:table TrainDriver:entity
@persist Speed SpeedMPH
@outputs Debugger:table
@strict

if (first() | duped())
{
    try
    {
        #include "e2shared/RailDriver/lib/smartEntityManagement"
        #include "e2shared/RailDriver/lib/sensors/speedometer"
        #include "e2shared/RailDriver/lib/controls/playerControls"
        #include "e2shared/RailDriver/lib/pidf"

        # Initialize Smart Entity Management.
        if (semInit())
        {
            #[
                Lock the E2, so that it can't be deleted or edited by anyone
                except you.

                This is not a security measure, as the E2 can still be
                deleted or edited by you. It is only a convenience measure,
                to prevent other users from accidentally or maliciously
                deleting or editing the E2, which would cause RailDriver to
                stop working.

                You can still update the E2 by using the "Update" button in
                the Remote Upload menu of the Expression 2 Tool Gun.
            ]#
            semLockE2()
            assert(semE2IsLocked(), "Failed to lock E2.")

            if (!semParentToLocomotive())
            {
                error("Failed to parent E2 to locomotive.")
            }

            Trucks = semFindTrucks()
            if (!semValidateTrucks(Trucks))
            {
                error("Locomotive trucks are not valid physics objects.")
            }
        }

        else
        {
            error("RailDriver: Failed to initialize Smart Entity Management.")
        }

        # Initialize Speedometer.
        Speedo = speedometerInit(Trucks[1, entity], Trucks[2, entity])
        if (Speedo:count() == 19)
        {
            speedometerSetFilterCutoffFrequency(Speedo, 1)
            speedometerStart(Speedo)
        }

        else
        {
            error("RailDriver: Failed to initialize Speedometer.")
        }

        # Setup throttle and brake maps.
        ThrottleMap["Throttle Input Type", string] = "Velocity"
        ThrottleMap["Velocity Setpoint", array] = array(0, 5, 15, 20, 30, 40, 60)
        ThrottleMap["Acceleration Setpoint", array] = array(0, 0.5, 1, 1.5, 2, 2.5, 3)
        ThrottleMap["Torque Setpoint", array] = array(0, 1000, 2000, 3000, 4000, 5000, 6000)
        BrakeMap["Brake Input Type", string] = "Velocity"
        BrakeMap["Deceleration Setpoint", array] = array(0, -0.5, -1, -1.5, -2, -2.5, -3)
        BrakeMap["Torque Setpoint", array] = array(0, -1000, -2000, -3000, -4000, -5000, -6000)

        # Setup default key bindings.
        local KeyboardControls = table()
        KeyboardControls["Increase Reverser", string] = "W"
        KeyboardControls["Decrease Reverser", string] = "S"
        KeyboardControls["Increase Throttle", string] = "D"
        KeyboardControls["Decrease Throttle", string] = "A"
        KeyboardControls["Increase Train Brake", string] = "left shift + D"
        KeyboardControls["Decrease Train Brake", string] = "left shift + A"
        KeyboardControls["Increase Engine Brake", string] = "left ctrl + D"
        KeyboardControls["Decrease Engine Brake", string] = "left ctrl + A"
        KeyboardControls["Emergency Brake", string] = "space"
        KeyboardControls["Horn", string] = "H"
        KeyboardControls["Bell", string] = "left alt"
        KeyboardControls["Headlights", string] = "F"
        KeyboardControls["Ditch Lights", string] = "left shift + F"

        # Set the driver to the player who spawned the E2.
        TrainDriver = owner()

        # Initialize Player Controls.
        PlayerControls = playerControlsInit(KeyboardControls, ThrottleMap, BrakeMap, TrainDriver)
        if (PlayerControls:count() == 0)
        {
            error("Failed to initialize Player Controls.")
        }

        timer("Debug", 100)
    }

    catch (Exception)
    {
        # If an exception is thrown, unlock the E2.
        # This is to prevent the E2 from being locked forever.
        if (semE2IsLocked())
        {
            semUnlockE2()
        }

        #[
            Notes from ZZ Cat:
            I tried to unparent the E2 & re-weld it to the locomotive, but it didn't work.
            I'm not sure why, but I'm not going to spend any more time on it.
            If you know how to fix this, please let me know by opening either an issue or a pull request on GitHub.
        ]#

        printColor(vec(255, 0, 0), "[RailDriver | Error]", vec(255, 255, 255), ": " + Exception)
    }
}

if (clk(speedometerGetClockId(Speedo)))
{
    try
    {
        speedometerClearAndRestartTimer(Speedo)

        local SpeedRaw = 0

        # Calculate the speed of the locomotive.
        SpeedRaw = speedometerGetSpeed(Speedo, 1)
        Speed = speedometerFilter(Speedo, SpeedRaw)

        # Apply a deadband to the speed.
        Speed = speedometerDeadband(Speedo, Speed)

    }

    catch (Exception)
    {
        printColor(vec(255, 0, 0), "[RailDriver | Error]", vec(255, 255, 255), ": " + Exception)
    }
}

if (keyClk())
{
    # Update the player controls.
    playerControlsUpdate(PlayerControls)

    try
    {
        # Check if the player controls are valid.
        if (!playerControlsValid(PlayerControls, SpeedFiltered))
        {
            error(playerControlsGetFaultMessage(PlayerControls))
        }
    }
    catch (Exception)
    {
        printColor(vec(255, 0, 0), "[RailDriver | Error]", vec(255, 255, 255), ": " + Exception)
    }
}

if (tickClk())
{
}

if (clk("Debug"))
{
    stoptimer("Debug")
    timer("Debug", 100)
    Debugger["Speed", number] = SpeedFiltered
    Debugger["Reverser", number] = playerControlsGetDirection(PlayerControls)
    Debugger["Throttle", number] = playerControlsGetThrottle(PlayerControls)
    Debugger["Brake", number] = playerControlsGetBrake(PlayerControls)
    Debugger["Emergency Brake", number] = playerControlsGetEmergencyBrake(PlayerControls)
}
